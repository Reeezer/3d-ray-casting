<!DOCTYPE html>
<html lang="en">
	<!-- https://webgl2fundamentals.org/webgl/lessons/webgl1-to-webgl2.html -->
	<!-- https://webgl2fundamentals.org/webgl/lessons/webgl-3d-perspective.html -->
	<!-- https://webgl2fundamentals.org/webgl/lessons/webgl-3d-camera.html -->

	<head>
		<meta charset="UTF-8" />
		<meta name="viewport" content="width=device-width, initial-scale=1.0" />
		<link rel="stylesheet" href="main.css" />
		<script src="./js/commonFunctions.js"></script>
		<script src="./js/gl-matrix-min.js"></script>
		<script src="./js/webglTools.js"></script>
		<title>Projet ray-casting</title>
		<style>
			* {
				margin: 0px;
			}

			.none {
				display: none;
			}
		</style>
	</head>

	<body>
		<canvas id="canvas" width="800" height="800" style="float: left"></canvas>

		<script src="dat.gui.min.js"></script>
		<script type="text/javascript" src="program.js"></script>
		<script type="text/javascript" src="config.js"></script>
		
        <script id="shader-vs" type="x-shader/x-vertex">#version 300 es
            in vec3 aVertexPosition;
            uniform mat4 uMVMatrix;
            uniform mat4 uPMatrix;
            precision mediump float;

            void main()
            {
                gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
            }
		</script>
        
		<script id="shader-fs" type="x-shader/x-fragment">#version 300 es
            precision mediump float;

            out vec4 outColor;

            const float EPSILON = 0.00001;
            const int MAX_LIGHT = 30;
            const int MAX_WALL = 100;
            const vec4 BLACK = vec4(0.0, 0.0, 0.0, 1.0);
            const float WALL_SIZE = 0.002;

            uniform vec2 u_WindowSize;
            uniform int u_Fading;

            uniform vec2 u_Lights[MAX_LIGHT];
            uniform vec3 u_LightsColor[MAX_LIGHT];
            uniform float u_LightsIntensity[MAX_LIGHT];
            uniform int u_LightsLength;

            uniform vec4 u_Walls[MAX_WALL];
            uniform int u_WallsLength;

            uniform bool u_drawWall;

            // https://en.wikipedia.org/wiki/Line%E2%80%93line_intersection#Given_two_points_on_each_line_segment
            bool intersect(vec2 p1, vec2 p2, vec2 p3, vec2 p4)
            {
                float x1 = p1.x;
                float y1 = p1.y;
                float x2 = p2.x;
                float y2 = p2.y;

                float x3 = p3.x;
                float y3 = p3.y;
                float x4 = p4.x;
                float y4 = p4.y;

                float denominator = (x1 - x2) * (y3 - y4) - (y1 - y2) * (x3 - x4);
                if(denominator == 0.0)
                {
                    return false;
                }

                float t = ((x1 - x3) * (y3 - y4) - (y1 - y3) * (x3 - x4)) / denominator;
                float u = -((x1 - x2) * (y1 - y3) - (y1 - y2) * (x1 - x3)) / denominator;

                return (t >= 0.0 && t <= 1.0 && u >= 0.0 && u <= 1.0);
            }

            bool canSeeLight(vec2 fragPos, vec2 lightPos, vec4 wall)
            {
                return !intersect(fragPos, lightPos, wall.xy, wall.zw);
            }

            vec2 scalarProjection(vec2 p1, vec2 p2, vec2 point)
            {
                // https://en.wikipedia.org/wiki/Scalar_projection
                vec2 a = point - p1;
                vec2 b = p2 - p1;
                vec2 bu = normalize(b);
                float s = dot(a, bu);

                vec2 projection = p1 + (bu * s);

                return projection;
            }

            bool inWall(vec2 fragPos, vec4 wall)
            {
                vec2 projection = scalarProjection(wall.xy, wall.zw, fragPos);
                vec2 projVect = projection - fragPos;

                float dist = length(projVect);
                vec2 longerVec = 2.0 * projVect;

                bool intersects = intersect(fragPos, fragPos + longerVec, wall.xy, wall.zw);

                return dist <= WALL_SIZE && intersects;
            }

            void main()
            {
                vec2 fragPos = gl_FragCoord.xy / u_WindowSize; // Window size needs to be changed to plane size maybe...
                fragPos.y = 1.0 - fragPos.y; //What why?

                vec3 color;
                if(u_drawWall)
                {
                    color = vec3(0.0);

                    // -----------------------------------------------------------------
                    // Check all lights
                    for(int i = 0; i < u_LightsLength; ++i)
                    {
                        vec2 lightPos = u_Lights[i];
                        float distFragLight = distance(fragPos, lightPos);

                        bool seeLight = true;
                        // Check all walls
                        for(int j = 0; j < u_WallsLength; ++j)
                        {
                            vec4 wall = u_Walls[j];
                            if(!canSeeLight(fragPos, lightPos, wall))
                            {
                                seeLight = false;
                                color = vec3(0.0);
                                break;
                            }
                        }
                        if(seeLight)
                        {
                            color += (u_LightsIntensity[i] * u_LightsColor[i]) / (distFragLight * float(u_Fading) * 0.4);
                        }
                    }
                    // -----------------------------------------------------------------
                }
                else 
                {
                    color = vec3(0.0);

                    //If fragment is part of a wall
                    for(int i = 0; i < u_WallsLength; ++i)
                    {
                        vec4 wall = u_Walls[i];

                        // Draw that fragment to wall color
                        if(inWall(fragPos, wall))
                        {
                            outColor = vec4(1.0,0,0,1.0); //TODO set the color to const maybe?
                            return;
                        }
                    }

                    //Check all lights
                    for(int i = 0; i < u_LightsLength; ++i)
                    {
                        vec2 lightPos = u_Lights[i];
                        float distFragLight = distance(fragPos, lightPos);

                        bool seeLight = true;
                        for(int j = 0; j < u_WallsLength; ++j)
                        {
                            vec4 wall = u_Walls[j];
                            if(!canSeeLight(fragPos, lightPos, wall))
                            {
                                seeLight = false;
                                break;
                            }
                        }
                        if(seeLight)
                        {
                            color += (u_LightsIntensity[i] * u_LightsColor[i]) / (distFragLight * float(u_Fading));
                        }
                    }
                }

                outColor = vec4(color, 1.0);
            }
		</script>
        <script type="text/javascript" src="useShader.js"></script>
		<script id="code-js" type="text/javascript" src="mouse.js"></script>
	</body>
</html>
